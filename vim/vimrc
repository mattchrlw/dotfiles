set encoding=utf-8
scriptencoding utf8
set nocompatible
" Basics {{{
" store path to vim folder in local variable
let s:vimdir = fnamemodify(resolve(expand('<sfile>:p')), ':h')
" set default 'runtimepath' (without ~/.vim folders)
let &runtimepath = printf('%s/vimfiles,%s,%s/vimfiles/after', $VIM, $VIMRUNTIME, $VIM)
" add the directory to 'runtimepath'
let &runtimepath = printf('%s,%s,%s/after', s:vimdir, &runtimepath, s:vimdir)

set hidden " Switch between buffers without saving
set clipboard^=unnamedplus,unnamed " Share OS/XServer clipboard
set backspace=indent,eol,start " make backspace work like most other apps
set mouse=a " Allow mouse usage in terminal vim
if has('mouse_sgr')
  set ttymouse=sgr " Allow mouse usage in tmux and ssh sessions
endif
set laststatus=2 " Always show status line
set autoindent " Copy indent from current line when starting a new line
set wildmode=longest,list " make auto completion on command line work like in shell
set updatetime=250

" f1  store file marks
" '   # of previously edited files to remember marks for
" :   # of lines of command history
" /   # of lines of search pattern history
" <   max # of lines for each register to be saved
" s   max # of Kb for each register to be saved
" h   don't restore hlsearch behavior
" %   save and restore the buffer list
if !isdirectory(s:vimdir . '/.tmp') | call mkdir(s:vimdir . '/.tmp') | endif
let &viminfo="'10,\"100,:50,n" . s:vimdir . '/.tmp/viminfo'

set scrolloff=5 " Scroll cursor offset

" remember cursor position
augroup CursorPosition
  autocmd!
  au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif
augroup END

set hlsearch " highlight search results
set ignorecase " ignore case in searches ... (\c\C override)
set smartcase " ... unless there are caps in the search
set incsearch " show where the pattern, as it was typed so far, matches

set lazyredraw " redraw only when we need to

" Store temporary files in a central spot
if !isdirectory(s:vimdir . '/.tmp/swap') | call mkdir(s:vimdir . '/.tmp/swap') | endif
let &directory=s:vimdir . '/.tmp/swap//,/var/tmp//,/tmp//,.'
if !isdirectory(s:vimdir . '/.tmp/undo') | call mkdir(s:vimdir . '/.tmp/undo') | endif
let &undodir=s:vimdir . '/.tmp/undo//,/var/tmp//,/tmp//,.'

set undofile " Enable vim to remember undo chains between sessions

" Ignore certain things
set wildignore+=.git,*/node_modules/*,*/deps/build/*,*/stack/*,*/deps/go/*,*/deps/node/*,*/_site/*

set noshowmode "get rid of the default mode indicator because we use airline

let g:mapleader = ',' " Leader key

set ttimeoutlen=50 "shorten pause when leaving insert mode

" Open help pages in new tab
cabbrev help tab help
" }}}
" Plugins {{{
" Install vim-plug if not available
augroup InstallPlug
  autocmd!

    if empty(glob(s:vimdir . '/autoload/plug.vim'))
     if !executable('curl') | echo 'curl needs to be installed' | quit | endif
      silent exe '!curl -fLo ' . s:vimdir . '/autoload/plug.vim --create-dirs ' .
        \ 'https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
      autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
    endif
augroup END

call plug#begin(s:vimdir . '/plugged')

Plug 'vim-airline/vim-airline'
Plug 'jacoborus/tender'
Plug 'airblade/vim-gitgutter'
Plug 'tpope/vim-fugitive'
Plug 'SirVer/ultisnips'
Plug 'epilande/vim-es2015-snippets'
Plug 'epilande/vim-react-snippets'
Plug 'mbbill/undotree'
Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all' }
Plug 'junegunn/fzf.vim'
if executable('cmake')
  Plug 'Valloric/YouCompleteMe', { 'do': './install.py --clang-completer',
                               \ 'for': [ 'cpp', 'c' ] }
  Plug 'rdnetto/YCM-Generator', { 'branch': 'stable'}
endif
Plug 'tomtom/tcomment_vim'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-unimpaired'
Plug 'wellle/targets.vim'
Plug 'ap/vim-css-color'
Plug 'skywind3000/asyncrun.vim'
Plug 'w0rp/ale'
Plug 'sheerun/vim-polyglot'
Plug 'lifepillar/vim-cheat40'

Plug 'Shougo/deoplete.nvim'
Plug 'roxma/nvim-yarp'
Plug 'roxma/vim-hug-neovim-rpc'

call plug#end()

" Better % command
runtime macros/matchit.vim
" }}}
" UI and colors {{{
if (has('termguicolors'))
 set termguicolors
endif

" Color Scheme
if !empty(glob(s:vimdir . '/plugged/tender'))
  colorscheme tender
  let g:airline_theme = 'tender'
  hi clear SignColumn
  hi VertSplit guifg=#808080 guibg=#282828
  hi Visual term=reverse cterm=reverse
  hi Search guifg=#eeeeee ctermfg=255 guibg=#f43753 ctermbg=203 gui=NONE cterm=NONE

  hi Conceal guibg=NONE ctermbg=NONE gui=NONE cterm=NONE
  hi Comment cterm=italic gui=italic " Italic comments.
  hi Todo gui=italic gui=italic " Italic comments in gui.
  hi xmlAttrib cterm=italic gui=italic term=underline ctermfg=81 guifg=#73cef4
endif

if &term =~# '256color'
  " disable Background Color Erase (BCE) so that color schemes
  " render properly when inside 256-color tmux and GNU screen.
  " see also http://snk.tuxfamily.org/log/vim-256color-bce.html
  set t_ut=
endif

" Highlight active line
set cursorline
hi clear CursorLine
hi CursorLineNR cterm=bold

" Show Line numbers
set number

" Visual line marking 80 characters
set colorcolumn=80

" Invisible characters
augroup InvisibleCharacters
  autocmd!
  set list
  autocmd BufEnter * set list listchars=tab:▸\ ,trail:≈,eol:¬
augroup END

highlight SpecialKey ctermfg=88 guifg=#870000

" }}}
" tabs vs. spaces, line breaks {{{
set expandtab " Spaces instead of tabs
set tabstop=2 " 2 spaces for each tab
set shiftwidth=2 " 2 spaces for indention

if has('linebreak')
  set breakindent
endif

" }}}
" Plugin configuration and mappings {{{

let g:ycm_confirm_extra_conf = 0
" YouCompleteMe
let g:ycm_error_symbol = '✗✗'
let g:ycm_warning_symbol = '∆∆'

" gitgutter
nnoremap <silent> <leader>g :call GitGutterDiffToggle()<cr>
let g:git_diff_last_commit = 0
function! GitGutterDiffToggle()
  if g:git_diff_last_commit
    let g:gitgutter_diff_base = ''
    execute 'GitGutter'
    let g:git_diff_last_commit = 0
  else
    let g:gitgutter_diff_base = 'HEAD~1'
    execute 'GitGutter'
    let g:git_diff_last_commit = 1
  endif
endfunction

" ale
let g:ale_linters = {
\  'javascript': ['eslint', 'flow'],
\  'tex': ['chktex'],
\  'c': [],
\  'cpp': ['clangtidy'],
\  'python': ['flake8'],
\  'java': [],
\}
let g:ale_fixers = {
\  'javascript': ['prettier'],
\  'json': ['prettier'],
\  'markdown': ['prettier'],
\  'cpp': ['clang-format'],
\  'python': ['autopep8'],
\  'sh': ['shfmt'],
\}
let g:ale_echo_msg_format = '[%linter%] %s [%severity%]'
let g:ale_javascript_prettier_options = '--trailing-comma es5 --single-quote'
let g:ale_sh_shfmt_options = '-i 2 -s'
let g:ale_c_clangformat_options = '-style="{BasedOnStyle: llvm, IndentWidth: 2, ColumnLimit: 100, AllowShortFunctionsOnASingleLine: None, KeepEmptyLinesAtTheStartOfBlocks: false}"'
let g:ale_python_autopep8_options = '-a -a --max-line-length=100'
let g:ale_python_flake8_options = '--max-line-length 100'
let g:ale_cpp_clangtidy_checks = ['clang-diagnostic-*', 'clang-analyzer-*']

nnoremap gp :ALEFix<CR>

let g:ale_sign_error = '✗✗'
let g:ale_sign_warning = '∆∆'
let g:ale_statusline_format = ['⨉ %d', '⚠ %d', '']

" UltiSnips
let g:UltiSnipsSnippetsDir = s:vimdir . '/UltiSnips'


" Airline
let g:airline_extensions = ['hunks', 'whitespace', 'tabline', 'ycm', 'ale',
                          \ 'branch', 'quickfix']

let g:airline#extensions#ycm#enabled = 1
let g:airline#extensions#ycm#error_symbol = '⨉ '
let g:airline#extensions#ycm#warning_symbol = '⚠ '

let g:airline#extensions#ale = 1
let g:airline#extensions#ale#error_symbol = '⨉ '
let g:airline#extensions#ale#warning_symbol = '⚠ '

let g:airline#extensions#tabline#enabled = 1
let g:airline#extensions#tabline#show_buffers = 1
let g:airline#extensions#tabline#buffer_idx_mode = 1

nmap <leader>1 <Plug>AirlineSelectTab1
nmap <leader>2 <Plug>AirlineSelectTab2
nmap <leader>3 <Plug>AirlineSelectTab3
nmap <leader>4 <Plug>AirlineSelectTab4
nmap <leader>5 <Plug>AirlineSelectTab5
nmap <leader>6 <Plug>AirlineSelectTab6
nmap <leader>7 <Plug>AirlineSelectTab7
nmap <leader>8 <Plug>AirlineSelectTab8
nmap <leader>9 <Plug>AirlineSelectTab9
nnoremap <leader>b :bp<CR>
nnoremap <leader>f :bn<CR>
nnoremap <leader>d :bd<CR>


" Don't show seperators
let g:airline_left_sep=''
let g:airline_right_sep=''

" clang-rename
let g:clang_rename_path = '/usr/local/opt/llvm/bin/clang-rename'
noremap <leader>ref :pyf s:vimdir . /scripts/clang-rename.py<cr>


" fzf
function! s:find_git_root()
  return system('git rev-parse --show-toplevel 2> /dev/null')[:-2]
endfunction

command! ProjectFiles execute 'Files' s:find_git_root()
nnoremap <C-p> :ProjectFiles<CR>
let g:fzf_colors =
\ { 'fg':      ['fg', 'Normal'],
  \ 'bg':      ['bg', 'Normal'],
  \ 'hl':      ['fg', 'Comment'],
  \ 'fg+':     ['fg', 'PreProc', 'CursorColumn', 'Normal'],
  \ 'bg+':     ['bg', 'CursorLine', 'CursorColumn'],
  \ 'hl+':     ['fg', 'Statement'],
  \ 'info':    ['fg', 'Operator'],
  \ 'border':  ['fg', 'Ignore'],
  \ 'prompt':  ['fg', 'Function'],
  \ 'pointer': ['fg', 'Exception'],
  \ 'marker':  ['fg', 'Keyword'],
  \ 'spinner': ['fg', 'Label'],
  \ 'header':  ['fg', 'Comment'] }

command! -bang -nargs=* Ag
  \ call fzf#vim#ag(<q-args>,
  \                 <bang>0 ? fzf#vim#with_preview('up:60%')
  \                         : fzf#vim#with_preview('right:50%:hidden', '?'),
  \                 <bang>0)


" vim-unimpaired vimcasts.org/episodes/bubbling-text/
" Bubble single lines
nmap <C-Up> [e
nmap <C-Down> ]e
" Bubble multiple lines
vmap <C-Up> [egv
vmap <C-Down> ]egv

" comment line
map <Leader>c gcc<ESC>

" toggle undotree
nnoremap <silent> <leader>u :UndotreeToggle<CR>
let g:undotree_SetFocusWhenToggle = 1
let g:undotree_WindowLayout = 2

" activate jsx syntax highlighting for .js files
let g:jsx_ext_required = 0

let g:deoplete#enable_at_startup = 1

" }}}
" General mappings {{{

" Saves time; maps the spacebar to colon
nmap <space> :
set pastetoggle=<leader>p " Toggle paste mode
" Keep curson in place when using J to join two lines
nnoremap J mzJ`z

if v:version > 703 || v:version == 703 && has('patch541')
  set formatoptions+=j
endif

" replay macro on Q instead of switching to Ex mode
nnoremap Q @@

" make Y consistent with D and C commands
nnoremap Y y$

" change rows not lines which is useful on wrapped lines
nnoremap j gj
nnoremap k gk

" In addition to <esc>, jj will exit to normal mode.
inoremap jj <ESC>
inoremap jk <ESC>

" highlight last inserted text
nnoremap gV `[v`]

" Get off my lawn
noremap <Left> <nop>
noremap <Right> <nop>
noremap <Up> <nop>
noremap <Down> <nop>

" Edit user .vimrc
nmap <Leader>v :e $MYVIMRC<CR>

" Source the vimrc file after saving it
augroup ReloadVimrc
  autocmd!
  autocmd bufwritepost *vimrc source $MYVIMRC |
        \ AirlineRefresh |
        \ redraw |
        \ echo 'vimrc reloaded!'
augroup END

" Toggle spell checking on and off with `,s`
nmap <silent> <leader>s :set spell!<CR>

" Clear search results and quicklist when hitting tab
nnoremap <silent> <tab> :nohlsearch<Bar>:echo<CR>:ccl<CR>

" Close preview window after completion
augroup closeprev
  autocmd CompleteDone * pclose
augroup END

" Show current file as HTML
nmap <Leader>h :TOhtml<CR>:w<cr>:!open %<CR>:q<CR>

augroup qf
    autocmd!
    " automatically close corresponding loclist when quitting a window
    autocmd QuitPre * if &filetype != 'qf' | silent! lclose | endif
augroup END

" http://vim.wikia.com/wiki/Identify_the_syntax_highlighting_group_used_at_the_cursor
map <F10> :echo "hi<" . synIDattr(synID(line("."),col("."),1),"name") . '> trans<'
\ . synIDattr(synID(line("."),col("."),0),"name") . "> lo<"
\ . synIDattr(synIDtrans(synID(line("."),col("."),1)),"name") . ">"<CR>

" }}}
" File type specific autocmds {{{

let g:tex_flavor = 'latex'
let g:vimtex_compiler_latexmk = {'callback' : 0}
nnoremap <F5> :call <SID>compile_and_run()<CR>
nnoremap <silent> <F6> :AsyncStop<CR>:call asyncrun#quickfix_toggle(10, 0)<CR>

augroup ASYNCRUN
    autocmd!
    " Automatically open the quickfix window
    autocmd User AsyncRunStart call asyncrun#quickfix_toggle(15, 1)
augroup END

function! s:compile_and_run()
  exec 'w'
  if &filetype ==# 'c'
    call MakeAndRun()
  elseif &filetype ==# 'cpp'
    call MakeAndRun()
  elseif &filetype ==# 'javascript.jsx'
    call RunWithNode()
  elseif &filetype ==# 'tex'
    exec 'AsyncRun latexmk % -pdf'
  elseif &filetype ==# 'python'
    exec 'AsyncRun -raw python3 %'
  endif
endfunction

function! RunWithNode()
  let l:prefix = join(split(system('npm prefix'), '\n'), '')
  let l:pkgpath = l:prefix.'/package.json'
  " if there is a package.json file
  if !empty(glob(l:pkgpath))
    let l:pkg = json_decode(join(readfile(l:pkgpath), "\n"))
    if (index(keys(l:pkg), 'main') >= 0) " if there is a key 'main'
      exec 'AsyncRun -raw node ' . l:prefix . '/' . l:pkg.main
      return
    endif
  endif
  " else fallback to currently opened file
  exec 'AsyncRun -raw node %<'
endfunction

function! MakeAndRun()
  if !empty(glob('Makefile')) " if a Makefile exists use it
    exec 'AsyncRun make && make run'
  else
    exec 'AsyncRun make %< && ./%<'
  endif
endfunction

" File type specific autocommands
augroup Filetypes
    autocmd!
    autocmd BufEnter *.hbt set filetype=html.handlebars syntax=mustache
    " in makefiles, don't expand tabs to spaces
    autocmd FileType make setlocal noexpandtab shiftwidth=8 softtabstop=0
    " turn on concealing for markdonw and latex
    autocmd FileType markdown set conceallevel=2
    autocmd FileType tex set conceallevel=2
augroup END
" }}}
" vim:foldmethod=marker:foldlevel=0
